# This is a collection of utilities used by the main CMake file
#
# A good (simple) overview of the use of CMake can be found at:
#   http://mirkokiefer.com/blog/2013/03/cmake-by-example/
#
# Then slightly more detail can be found at:
#   http://www.cmake.org/cmake-tutorial/
#
# With a multi-(sub)-project structure we need to use project import/export
# This has a good overview at:
#   http://www.cmake.org/Wiki/CMake/Tutorials/Exporting_and_Importing_Targets
#
# To create targets in the root project for each subproject see:
#  http://stackoverflow.com/questions/25019867/create-a-custom-target-that-call-subproject-custom-targets


# Set the default build type
#
function(setDefaultBuildType buildType cFlags)
  if (NOT DEFINED CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE ${buildType})
  endif (NOT DEFINED CMAKE_BUILD_TYPE)
  set(CMAKE_C_FLAGS "${cFlags} ${CMAKE_C_FLAGS}" CACHE STRING "" FORCE )
  add_definitions(${CMAKE_C_FLAGS})
endfunction(setDefaultBuildType)

# Load the LLVM utilities
#
macro(loadLLVM)
  if (DEFINED LLVM_DIR)
    find_package(LLVM REQUIRED CONFIG)
    include_directories(${LLVM_INCLUDE_DIRS})
    add_definitions(${LLVM_DEFINITIONS})
    llvm_map_components_to_libnames(llvm_libs support core irreader)
    message(STATUS "Found LLVM ${LLVM_PACKAGE_VERSION}")
    message(STATUS "Using LLVMConfig.cmake in: ${LLVM_DIR}")
  endif(DEFINED LLVM_DIR)
endmacro(loadLLVM)


# Transform source and test files into a format suitable for use with 
# Doxygen.
macro(transformDoxygen)
  add_custom_target(transformDoxygenSrcs
    COMMAND ${CMAKE_SOURCE_DIR}/bin/transformDoxygen ${CMAKE_SOURCE_DIR}/src ${CMAKE_SOURCE_DIR}/tests
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    COMMENT "Transforming source and tests for API documentation with Doxygen" VERBATIM
  )
  add_dependencies(doc transformDoxygenSrcs )
endmacro(transformDoxygen)

# Load and Configure Doxygen
#
# add a target to generate API documentation with Doxygen
macro(configureDoxygen)
  find_package(Doxygen)
  if(DOXYGEN_FOUND)
    configure_file(
      ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in 
      ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile
      @ONLY
    )
    file(MAKE_DIRECTORY 
      "${CMAKE_BINARY_DIR}/docs" 
      "${CMAKE_BINARY_DIR}/docs/api"
    )
    add_custom_target(doc
      ${DOXYGEN_EXECUTABLE} 
      ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile
      WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
      COMMENT "Generating API documentation with Doxygen" VERBATIM
    )
  endif(DOXYGEN_FOUND)
endmacro(configureDoxygen)

# DEFINE tests
#
function(defineTestsFor libraryName includeDirectories testLibraries)
  #
  # Collect all of the required include files into the testSources
  #
  list(APPEND testSources "")
  foreach(includeDir IN LISTS includeDirectories)
    file(GLOB_RECURSE includeHeaders "${CMAKE_SOURCE_DIR}/${includeDir}/*.h")
    list(APPEND testSources ${includeHeaders})
  endforeach(includeDir)
  #
  # Now add in the sources from this project/directory
  #
  file(GLOB_RECURSE sourceFiles "*.h" "*.c*" )
  list(APPEND testSources ${sourceFiles})
  #
  # Add the test executable
  #
  add_executable("${libraryName}Tests" ${testSources})
  #
  # Add in the compile options
  #
  foreach(includeDir IN LISTS includeDirectories)
    target_compile_options("${libraryName}Tests"
      PRIVATE "-I${CMAKE_SOURCE_DIR}/${includeDir}")
  endforeach(includeDir)
  target_compile_options("${libraryName}Tests"
    PRIVATE "-I${CMAKE_SOURCE_DIR}/${sourceDirectory}")
  target_compile_options("${libraryName}Tests" 
    PRIVATE "-Wall" "-std=c++11")
  #
  # Add in the libraries
  #
  target_link_libraries("${libraryName}Tests" ${libraryName})
  foreach(testLibrary IN LISTS testLibraries)
    target_link_libraries("${libraryName}Tests" ${testLibrary})
  endforeach(testLibrary)
  target_link_libraries("${libraryName}Tests" ${llvm_libs})
  #
  # mark this executable as a test
  #
  add_test(${libraryName} "${libraryName}Tests")
endfunction(defineTestsFor)

# DEFINE library
#
function(defineLibrary libraryName sourceDirectory includeDirectories)
  #
  # Collect all of the required include files into the testSources
  #
  list(APPEND librarySources "")
  foreach(includeDir IN LISTS includeDirectories)
    file(GLOB_RECURSE includeHeaders "${CMAKE_SOURCE_DIR}/${includeDir}/*.h")
    list(APPEND librarySources ${includeHeaders})
  endforeach(includeDir)
  #
  # Now add in the sources from this project/directory
  #
  file(GLOB_RECURSE sourceFiles "*.h" "*.c*" )
  list(APPEND librarySources ${sourceFiles})
  #
  # Add the library
  #
  add_library(${libraryName} ${librarySources})
  #
  # Add the compile options
  #
  foreach(includeDir IN LISTS includeDirectories)
    target_compile_options(${libraryName} PRIVATE "-I${CMAKE_SOURCE_DIR}/${includeDir}")
  endforeach(includeDir)
  target_compile_options(${libraryName} PRIVATE "-I${CMAKE_SOURCE_DIR}/${sourceDirectory}")
  target_compile_options(${libraryName} PRIVATE "-Wall")
#  target_compile_options(${libraryName} PRIVATE "-Wall" "-std=c++11")
endfunction(defineLibrary)

# DEFINE executable
#
function(defineExecutable executableName sourceDirectories libraries)
  file(GLOB_RECURSE executableSources
    "${sourceDirectories}/*.h"
    "${sourceDirectories}/*.c*"
  )
  add_executable(${executableName} 
    ${executableSources} 
  )
  target_link_libraries(${executableName} ${libraries})
  target_link_libraries(${executableName} ${llvm_libs})
endfunction(defineExecutable)

